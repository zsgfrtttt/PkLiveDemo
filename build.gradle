/*
 * Copyright (c) 2021 NetEase, Inc.  All rights reserved.
 * Use of this source code is governed by a MIT license that can be found in the LICENSE file.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven{
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        flatDir {
            dirs '../aars'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    compileSdkVersion = 30
    buildToolsVersion = "30.0.0"
    minSdkVersion = 19
    targetSdkVersion = 30

    nertcVersion = "4.1.1"
    nimVersion = '8.3.1'
    playerVersion = '2.7.1'
    playerGslbVersion = '1.1.0'
    aLogVersion = '1.0.0'

    nertcSdk = "com.netease.yunxin:nertc:$nertcVersion"
    imSdk = "com.netease.nimlib:basesdk:$nimVersion"
    avsignallingSdk = "com.netease.nimlib:avsignalling:$nimVersion"
    chatRoomSdk = "com.netease.nimlib:chatroom:$nimVersion"
    utilcodex = "com.blankj:utilcodex:1.30.6"
    playerSdk = "com.netease.nimlib:neliveplayer:$playerVersion"
    playerGslbSdk = "com.netease:gslb:$playerGslbVersion"
    aLog = "com.netease.yunxin.kit:alog:$aLogVersion"

    ndkAbis = [
            'armeabi-v7a',
            'arm64-v8a',
    ]
}

ext {
    AppKey = '9ee2101a195b4044c4002d1972156396'
    BaseUrl = 'https://yiyong.netease.im/'
}

def loadLocalConfig() {
    String env = System.getProperty("env", "test")
    if (env != "test") env = "online"
    println "env=$env"
    def propertiesFile = file("config/${env}.properties")
    if (!propertiesFile.exists()) {
        println "Local properties don't exist."
        return
    }
    Properties config = new Properties()
    config.load(propertiesFile.newInputStream())
    this.AppKey = config.getProperty('APP_KEY')
    this.BaseUrl = config.getProperty('BASE_URL')
}

loadLocalConfig()